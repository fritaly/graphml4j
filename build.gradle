buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'nexus'

defaultTasks 'clean', 'build'

repositories {
  // Fetch the dependencies from Maven Central
  mavenCentral()
}

sourceCompatibility = targetCompatibility = '1.8'

dependencies {
  compile 'commons-lang:commons-lang:2.4'
  compile 'net.java.dev.stax-utils:stax-utils:20070216@jar'

  testCompile localGroovy()
  testCompile 'junit:junit:4.8.1'
}

jar {
  manifest {
    attributes += [ "Artifact-Group": "${project.group}" ]
    attributes += [ "Artifact-Name": "${project.name}" ]
    attributes += [ "Artifact-Version": "${project.version}" ]
    attributes += [ "Built-On": "${new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss (Z)').format(new Date())}" ]
    attributes += [ "Built-By": "${System.properties['user.name']}" ]
    attributes += [ "Java-Version": "${System.properties['java.version']}" ]
  }
}

sourceSets {
  samples {
    java {
      // The samples need the main classes to compile & the compile dependencies
      compileClasspath += sourceSets.main.output + configurations.compile
    }
  }
}

// Compile the samples before packaging them
task distribution(type: Zip, dependsOn: samplesClasses) {
  classifier = 'dist'
  from jar, {
    into 'lib'
  }
  from javadoc, {
    into 'docs/javadoc'
  }
  from sourceSets.samples.allSource, {
    into 'samples'
  }
  from file('LICENSE')
}

artifacts {
  // Upload the distribution file too
  archives tasks.distribution
}

jacoco {
  toolVersion = "0.7.1.201405082137"
}

build.dependsOn += [distribution, jacocoTestReport]

modifyPom {
    project {
        name 'Graphml4J'
        description 'A simple Java API for generating GraphML files for yEd.'
        url 'https://github.com/fritaly/graphml4j'
        inceptionYear '2014'
        packaging 'jar'

        scm {
            url 'https://github.com/fritaly/graphml4j'
            connection 'scm:git:https://github.com/fritaly/graphml4j.git'
            developerConnection 'scm:git@github.com:fritaly/graphml4j.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'fritaly'
                name 'Francois Ritaly'
                email 'francois.ritaly@gmail.com'
            }
        }
    }
}

nexus {
    sign = false
}
